buildscript {
  ext {
    springBootVersion = '2.0.0.RELEASE'
  }
  repositories {
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath 'net.researchgate:gradle-release:2.6.0'
  }
}

if (System.getenv('VERSION')) {
  version = System.getenv('VERSION')
}
if (System.getenv('BUILD_NUMBER')) {
  version = version + '-' + System.getenv('BUILD_NUMBER')
}


apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'net.researchgate.release'

jar {
  baseName = 'pttg-ip-audit'
  if (System.getenv('GIT_COMMIT')) {
    version = version + '.' + System.getenv('GIT_COMMIT')
  }
  manifest {
    attributes 'Implementation-Title': baseName,
               'Implementation-Version': version
  }
}

checkstyleTest.enabled = false
checkstyle {
  toolVersion = "5.9"
  ignoreFailures = false
}
findbugs {
  ignoreFailures = false
  effort = "min"
}

tasks.withType(FindBugs) {
  excludeFilter = file('config/findbugs/exclude-filter.xml')
  reports {
    xml.enabled false
    html.enabled true
  }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile("org.springframework.boot:spring-boot-starter-web") {
    exclude module: "spring-boot-starter-tomcat"
  }

  compile group: 'org.springframework.boot',          name: 'spring-boot-starter-jetty'
  compile group: 'org.springframework.boot',          name: 'spring-boot-starter-actuator'
  compile group: 'org.springframework.boot',          name: 'spring-boot-starter-data-jpa'

  compile group: 'com.google.guava',                  name: 'guava',                      version: '23.4-jre'

  compile group: 'com.fasterxml.jackson.datatype',    name: 'jackson-datatype-jsr310',    version: '2.8.3'

  compile group: 'org.flywaydb',                      name: 'flyway-core',                version: '5.1.1'

  compile group: 'net.logstash.logback',              name: 'logstash-logback-encoder',   version: '5.0'

  compile 'org.hsqldb:hsqldb'
  compile 'org.postgresql:postgresql:9.4.1211.jre7'

  compile group: 'org.json', name: 'json', version: '20170516'

  compileOnly 'org.projectlombok:lombok:1.16.12'

  testCompile group: 'org.springframework.boot',  name: 'spring-boot-starter-test'
  testCompile group: 'org.mockito',               name: 'mockito-core',             version: '2.16.0'

  testCompile group: 'junit',                     name: 'junit',                    version: '4.12'
  testCompile group: 'org.assertj',               name: 'assertj-core',             version: '3.5.2'
  testCompile group: 'com.jayway.jsonpath',       name: 'json-path'
  testCompile group: 'org.apache.commons',        name: 'commons-io',               version: '1.3.2'
  testCompile group: 'org.skyscreamer',           name: 'jsonassert',               version: '1.5.0'
  testCompile group: 'com.github.tomakehurst',    name: 'wiremock',                 version: '2.4.1'

  testCompile group: 'com.google.code.findbugs',  name: 'annotations',                version: '3.0.1'
}

gradle.startParameter.continueOnFailure = false

release {
  preTagCommitMessage = '[Gradle Release Plugin] (EE-9726) [CI SKIP] - pre tag commit: '
  tagCommitMessage = '[Gradle Release Plugin] (EE-9726) - creating tag: '
  newVersionCommitMessage = '[Gradle Release Plugin] (EE-9726) [CI SKIP] - new version commit: '
}

task updateChart() {
  doLast {
    updateChart_steps()
  }
}

void updateChart_steps() {
  exec {
    commandLine 'sh', '-c', 'sh update-chart.sh'
  }
}

tasks.preTagCommit.dependsOn updateChart
