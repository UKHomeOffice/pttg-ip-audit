buildscript {
  ext {
    springBootVersion = '2.6.0'
  }
  repositories {
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath 'net.researchgate:gradle-release:2.6.0'
  }
}

if (System.getenv('VERSION')) {
  version = System.getenv('VERSION')
}
if (System.getenv('BUILD_NUMBER')) {
  version = version + '-' + System.getenv('BUILD_NUMBER')
}


apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'checkstyle'
apply plugin: 'net.researchgate.release'

jar {
  baseName = 'pttg-ip-audit'
  if (System.getenv('GIT_COMMIT')) {
    version = version + '.' + System.getenv('GIT_COMMIT')
  }
  manifest {
    attributes 'Implementation-Title': baseName,
               'Implementation-Version': version
  }
}

checkstyleTest.enabled = false
checkstyle {
  toolVersion = "5.9"
  ignoreFailures = false
}

sourceCompatibility = 11

repositories {
  mavenCentral()
}

dependencies {
  implementation("org.springframework.boot:spring-boot-starter-web") {
    exclude module: "spring-boot-starter-tomcat"
  }

  implementation group: 'org.springframework.boot',          name: 'spring-boot-starter-jetty'
  implementation group: 'org.springframework.boot',          name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot',          name: 'spring-boot-starter-data-jpa'

  implementation group: 'com.google.guava',                  name: 'guava',                      version: '23.4-jre'

  implementation group: 'com.fasterxml.jackson.datatype',    name: 'jackson-datatype-jsr310',    version: '2.8.3'

  implementation group: 'org.flywaydb',                      name: 'flyway-core',                version: '5.2.4'

  implementation group: 'net.logstash.logback',              name: 'logstash-logback-encoder',   version: '5.0'

  implementation 'org.hsqldb:hsqldb'
  implementation 'org.postgresql:postgresql:42.2.5'

  implementation group: 'org.json', name: 'json', version: '20170516'

  compileOnly 'org.projectlombok:lombok:1.18.12'
  annotationProcessor 'org.projectlombok:lombok:1.18.12'

  testImplementation group: 'org.springframework.boot',  name: 'spring-boot-starter-test'
  testImplementation group: 'org.mockito',               name: 'mockito-core',             version: '2.16.0'

  testImplementation group: 'junit',                     name: 'junit',                    version: '4.12'
  testImplementation group: 'org.assertj',               name: 'assertj-core',             version: '3.5.2'
  testImplementation group: 'com.jayway.jsonpath',       name: 'json-path'
  testImplementation group: 'org.apache.commons',        name: 'commons-io',               version: '1.3.2'
  testImplementation group: 'org.skyscreamer',           name: 'jsonassert',               version: '1.5.0'
  testImplementation group: 'com.github.tomakehurst',    name: 'wiremock',                 version: '2.4.1'

}

gradle.startParameter.continueOnFailure = false

release {
  preTagCommitMessage = '[Gradle Release Plugin] (EE-9726) [CI SKIP] - pre tag commit: '
  tagCommitMessage = '[Gradle Release Plugin] (EE-9726) - creating tag: '
  newVersionCommitMessage = '[Gradle Release Plugin] (EE-9726) [CI SKIP] - new version commit: '
}

task updateChart() {
  doLast {
    exec {
      commandLine 'sh', './update-chart.sh'
    }
  }
}

tasks.preTagCommit.dependsOn updateChart
